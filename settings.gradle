include ':library-db'
include ':library-aop'
include ':library-common'
include ':library-network'
include ':library-widget'
include ':library-base'

// 开发环境，以下三个Module接入时务必移除
include(":component-api")
project(":component-api").projectDir = new File("../Component/component-api")
include(":component-compiler")
project(":component-compiler").projectDir = new File("../Component/component-compiler")
include(":component-annotation")
project(":component-annotation").projectDir = new File("../Component/component-annotation")


buildscript {
    repositories {
        google()
        jcenter()
        maven { url "file://${rootProject.projectDir.parentFile.absolutePath}/Component/libraries/repo" }  // 开发环境，接入时务必移除
        maven { url "https://wannng.bintray.com/maven" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.kunpeng.tools.gradle:component-plugin:0.0.7'
    }
}

apply plugin: 'component-settings'
component {
    dev_env = true  // 开发环境，接入时务必移除

    app {
        projectName = 'app'
        projectDir = new File("./app")
        dependencies = [
                module("Mine"),
                module("Login"),
                module("Home"),
                module("Project"),
                module("Web"),
                module("Publics"),
                module("Square"),
        ]
    }

    runAppConfig {
        applicationId = "test.component"
        versionCode = 1
        versionName = "1.0"
    }


    module("Home") {
        projectName = "module-home"
        dependencies = [
                module("Web"),
        ]
    }

    module("Project") {
        projectName = "module-project"
        dependencies = [
                module("Web"),
        ]
    }

    module("Web") {
        projectName = "module-web"
    }

    module("Publics") {
        projectName = "module-public"
        dependencies = [
                module("Web"),
        ]
    }

    module("Square") {
        projectName = "module-square"
        dependencies = [
                module("Web"),
        ]
    }

    module("Mine") {
        projectName = "module-mine"
        dependencies = [
                module("Login"),
                module("Web"),
        ]
    }

    module("Login") {
        projectName = "module-login"

//        runApp = false
//        runAppConfig {
//            applicationId = "com.kunpeng.login"
//            mainActivity = "com.kunpeng.login.ui.LoginActivity"
//        }
    }

}