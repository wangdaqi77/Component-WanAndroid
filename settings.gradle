include ':library-db'
include ':library-common'
include ':library-network'
include ':library-widget'
include ':library-base'

def dev = false
def localProperties = new Properties()
def localPropertiesFile = new File(buildscript.sourceFile.parentFile, "local.properties")

if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }

    if (localProperties.getProperty("component.dev") != null) {
        dev = localProperties.getProperty("component.dev").toBoolean()
    }
}


if (dev) {
    include(":component-api")
    project(":component-api").projectDir = new File("../Component/component-api")
    include(":component-compiler")
    project(":component-compiler").projectDir = new File("../Component/component-compiler")
    include(":component-annotation")
    project(":component-annotation").projectDir = new File("../Component/component-annotation")
}


buildscript {
    repositories {
        google()
        jcenter()
        maven { url "file://${rootProject.projectDir.parentFile.absolutePath}/Component/libraries/repo" }  // 开发环境，接入时务必移除
        maven { url "https://wannng.bintray.com/maven" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.kunpeng.tools.gradle:component-plugin:0.1.0'
    }
}

apply plugin: 'component-settings'
component {
    dev_env = dev

    app {
        projectName = 'app'
        projectDir = new File("./app")
        dependencies = [
                module("Mine"),
                module("Login"),
                module("Home"),
                module("Project"),
                module("Web"),
                module("Publics"),
                module("Square"),
                
        ]
    }

    module("Home") {
        projectName = "module-home"
        dependencies = [
                module("Web"),
                
        ]
    }

    module("Project") {
        projectName = "module-project"
        dependencies = [
                module("Web"),
       ]
    }

    module("Web") {
        projectName = "module-web"
//        runApp = false // 运行app开关
//        runAppConfig {
//            enabled = false // 当runApp=true时, enabled=false, applicationId等配置需要在对应项目的build.gradle配置
//        }
    }

    module("Publics") {
        projectName = "module-public"
        dependencies = [
                module("Web"),
        ]
    }

    module("Square") {
        projectName = "module-square"
        dependencies = [
                module("Web"),
                
        ]
    }

    module("Mine") {
        projectName = "module-mine"
        dependencies = [
                module("Login"),
                module("Web"),
        ]
    }

    module("Login") {
        projectName = "module-login"

        runApp = true // 运行app开关
        runAppConfig {
            enabled = true  // 当runApp=true时, enabled=false, applicationId等配置需要在对应项目的build.gradle配置
            applicationId = "x.x.login"
            mainActivity = "com.zlx.module_login.activity.LoginAc"
        }
    }

}